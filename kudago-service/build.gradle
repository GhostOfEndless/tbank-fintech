plugins {
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'io.freefair.lombok' version '8.10'
    id 'java'
    id 'jacoco'
    id 'checkstyle'
}

group = 'com.example'
version = '1.0-SNAPSHOT'

checkstyle {
    configFile = file("../config/checkstyle/checkstyle.xml")
}

repositories {
    mavenCentral()
}

dependencies {
    // spring starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // other spring libs
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
    implementation 'org.springframework.retry:spring-retry:2.0.10'
    implementation 'org.postgresql:postgresql:42.7.4'

    //    jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.6'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // utils
    implementation 'com.google.guava:guava:33.3.1-jre'
    implementation 'org.jspecify:jspecify:1.0.0'
    annotationProcessor 'org.hibernate:hibernate-jpamodelgen:6.6.1.Final'
    implementation project(':log-aspect')

    // logging and metrics
    implementation 'net.logstash.logback:logstash-logback-encoder:8.0'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.14.1'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.4.0'

    // tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter:1.20.3'
    testImplementation 'org.wiremock.integrations.testcontainers:wiremock-testcontainers-module:1.0-alpha-14'
    testImplementation 'org.wiremock:wiremock-standalone:3.9.1'
    testImplementation 'org.testcontainers:postgresql:1.20.3'
    testImplementation 'org.liquibase:liquibase-core:4.29.2'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    jvmArgs '-XX:+EnableDynamicAgentLoading'
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/example/entity/",
                    "com/example/controller/payload/",
                    "com/example/exception/",
                    "com/example/client/dto/",
                    "**/Application**"
            ])
        }))
    }
}